---
layout:     post
title:      NCl脚本
subtitle:   整层水汽通量
date:       2020-02-25
author:     Qiu
header-img: img/ncl.jpg
catalog: true
tags:
    - NCL
    - 水汽通量
---
# NCl画整层水汽通量的图
```
;----------------------------------------------------------
; Time    :   2020/01/14 17:20:33
; Author  :   ChiQ 
; 盛夏多年平均水汽通量场
;----------------------------------------------------------
begin
    q_f = addfile("../data/Midsummer/midsummer_water_vapor_flux.nc", "r")
    uq = q_f->uqm
    vq = q_f->vqm

    uq_clim = dim_avg_n_Wrap(uq, 0)
    vq_clim = dim_avg_n_Wrap(vq, 0)

    ;--------------------------------------------------------------------------plot
    minLon = 40
    maxLon = 140
    minLat = -10
    maxLat = 50

    png_out = "./img/clim_Q"
    wks_type = "pdf"  
    wks = gsn_open_wks(wks_type, png_out)
    
    res                         = True
    res@gsnDraw                 = False
    res@gsnFrame                = False
    res@vpWidthF                = .8
    res@gsnAddCyclic            = False
    res@gsnLeftString           = ""
    res@gsnRightString          = ""
   
    ; --map
    res@mpFillOn                = False  
    res@mpLimitMode             = "LatLon"
    res@mpMinLonF               = minLon
    res@mpMaxLonF               = maxLon
    res@mpMinLatF               = minLat
    res@mpMaxLatF               = maxLat
    res@mpGridAndLimbOn         = False

    ; --ticks
    res@tmXTOn                  = False
    res@tmYROn                  = False
    res@tmXBMode                = "Explicit"
    res@tmXBValues              = ispan(40, 140, 20)
    res@tmXBLabels              = (/"40~S~o~N","60~S~o~N~","80~S~o~N~","100~S~o~N~","120~S~o~N~","140~S~o~N~E"/)
    res@tmXBLabelFontHeightF    = 0.023
    res@tmYLMode                = "Explicit"
    res@tmYLValues              = ispan(-10, 50, 20)
    res@tmYLLabels              = (/"10~S~o~N~S","10~S~o~N~   ","30~S~o~N~   ","50~S~o~N~N"/)
    res@tmYLLabelFontHeightF    = 0.023

    ;--vctor 
    res@vcGlyphStyle            = "CurlyVector"
    res@vcRefMagnitudeF         = 300 
    res@vcRefLengthF            = 0.045
    ; res@vcMinDistanceF        = 0.019  ; 稀疏风矢量
    res@vcRefAnnoOrthogonalPosF = -.21
    res@vcLineArrowThicknessF   = 1
    res@vcMinAnnoAngleF         = 1
    res@vcRefAnnoSide           = "Top"
    res@vcRefAnnoPerimOn        = False  ; 不绘制参考矢量的轮廓
    res@vcRefAnnoString2On      = False


    vcPlot = gsn_csm_vector_map(wks, uq_clim, vq_clim, res)
    ; --shapefile
    TP_shp                 = "../shapefile/Qinghai-Tibet_Plateau/tibetan.shp"
    lnres                  = True
    lnres@gsLineColor      = "black"
    lnres@gsLineThicknessF = 3   
    TP = gsn_add_shapefile_polylines(wks,vcPlot,TP_shp,lnres)
    
    ; --单个图加a,b,c
    resP                                  = True
    resP@gsnPanelFigureStrings            = (/"(a)"/)
    resP@amJust                           = "TopLeft"
    ; resP@gsnScale                         = True
    resP@gsnPanelFigureStringsPerimOn     = False
    resP@gsnPanelFigureStringsFontHeightF = 0.03
    gsn_panel(wks, vcPlot, (/1,1/), resP)

end
```
![图片](img/clim_Q.png)

