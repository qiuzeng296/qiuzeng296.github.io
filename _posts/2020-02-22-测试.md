---
layout:     post
title:      关于NCl中显著性打点和ShapeFile画成阴影
subtitle:   “”
date:       2020-02-22
author:     Qiu
header-img: img/post-bg-ios9-web.jpg
catalog: true
tags:
    - NCL
    - ShapeFile
---
# 关于NCl中显著性打点和ShapeFile画成阴影
```
begin
    hgt_f = addfile("../data/Midsummer/midsummer_1961_2017_h_u_v.nc", "r")
    hgt = hgt_f->hgt

    h_yr = (/1962, 1988, 1998, 2000, 2014, 2016/)
    l_yr = (/1967, 1975, 1982, 1983, 1994, 1997, 2006/)

    h_ind = get1Dindex(hgt&year, h_yr)
    l_ind = get1Dindex(hgt&year, l_yr)

    h_hgt = hgt(h_ind, {500}, :, :)
    l_hgt = hgt(l_ind, {500}, :, :)

    h_hgt_clim = dim_avg_n_Wrap(h_hgt, 0)
    l_hgt_clim = dim_avg_n_Wrap(l_hgt, 0)

    hgt_dif = h_hgt_clim - l_hgt_clim
    copy_VarMeta(h_hgt(1,:,:), hgt_dif)

    hgt_dif = smth9_Wrap(hgt_dif, 0.50,  0.25, False) 
    
    ; --雅江盛夏降水偏多年和偏少t检验的p值
    p_f = addfile("../data/Midsummer/midsummer_rain_h_l_pdif.nc", "r")
    hgt_dif_p = p_f->hgt({500}, :, :)
    
    ; -------------------------------------------plot

    minLon = -40.
    maxLon = 110.
    minLat = 10.
    maxLat = 75.


    png_out = "./img/500hPa_h_dif2"
    wks_type = "png" 
    wks = gsn_open_wks(wks_type, png_out)
    
    res                                 = True
    res@gsnDraw                         = False
    res@gsnFrame                        = False
    res@gsnLeftString                   = ""
    res@gsnRightString                  = ""

    ; --map
    map                                 = res
    map@mpFillOn                        = False  
    map@mpLimitMode                     = "LatLon"
    map@mpMinLonF                       = minLon
    map@mpMaxLonF                       = maxLon
    map@mpMinLatF                       = minLat
    map@mpMaxLatF                       = maxLat
    map@mpGridAndLimbOn                 = False
    map@tmXTOn                          = False
    map@tmYROn                          = False
    map@tmXBMode                        = "Explicit"
    map@tmXBValues                      = (/-30, 0, 30, 60, 90/)
    map@tmXBLabels                      = (/"30~S~o~N~W","0~S~o~N~","30~S~o~N~E","60~S~o~N~","90~S~o~N~"/)
    map@tmXBLabelFontHeightF            = 0.015
    ; map@tmXBLabelFont                   = 25
    map@tmYLMode                        = "Explicit"
    map@tmYLValues                      = (/15,30,45,60,75/)
    map@tmYLLabels                      = (/"15~S~o~N~   ","30~S~o~N~   ","45~S~o~N~   ","60~S~o~N~   ","70~S~o~N~N"/)
    map@tmYLLabelFontHeightF            = 0.015

    ; --pValues
    ; --等值线间隔一定要设置为0.01，然后ShadeLtContour中val 设置成0.06
    cnres1                              = res
    cnres1@gsnAddCyclic                 = True
    cnres1@cnLevelSpacingF              = 0.01
    ; cnres1@cnMonoFillScale              = False  
    ; cnres1@cnFillScales                 = 0.3 
    cnres1@cnFillOn                     = False
    cnres1@cnLinesOn                    = False
    cnres1@cnLineLabelsOn               = False
    cnres1@cnInfoLabelOn                = False             ; 关闭图右下方的等值线信息标签
    cnres1@lbLabelBarOn                 = False             ; 关闭色标


    ; --hgt dif
    cnres2                              = res
    cnres2@gsnAddCyclic                 = True
    cnres2@cnFillOn                     = True
    cnres2@cnLinesOn                    = False
    cnres2@cnLineLabelsOn               = False
    cnres2@cnSmoothingOn                = True
    cnres2@cnRasterSmoothingOn          = True
    cnres2@cnSmoothingDistanceF         = 0.01
    cnres2@cnSmoothingTensionF          = 1
    cnres2@cnFillPalette                = "GMT_polar"
    cnres2@lbOrientation                = "Horizontal"
    cnres2@lbLabelFontHeightF           = 0.012
    cnres2@cnInfoLabelOn                = False
    cnres2@lbLabelAutoStride             = True
    cnres2@pmLabelBarOrthogonalPosF     = .10


    basePlot = gsn_csm_map(wks,map)   
    symMinMaxPlt(hgt_dif, 30, False, cnres2)
    cnPlot = gsn_csm_contour(wks, hgt_dif, cnres2)
    cnPPlot = gsn_csm_contour(wks, hgt_dif_p, cnres1)

    cnPPlot = ShadeLtContour(cnPPlot, 0.06, 10)
    overlay(basePlot, cnPlot)
    overlay(basePlot, cnPPlot)
    
    TibetPlateau_shp          = "../shapefile/Qinghai-Tibet_Plateau/tibetan.shp"
    lncnres1                  = True
    lncnres1@gsLineColor      = "black"
    lncnres1@gsFillColor      = "gray"
    lncnres1@gsLineThicknessF = 3
    TibetPlateau_Line = gsn_add_shapefile_polygons(wks,basePlot,TibetPlateau_shp,lncnres1)

     ; -- 图注
    resP                                  = True
    resP@gsnPanelFigureStrings            = (/"(a)"/)
    resP@amJust                           = "TopLeft"
    resP@gsnPanelFigureStringsFontHeightF = 0.02
    gsn_panel(wks, basePlot, (/1,1/), resP)


end
```


